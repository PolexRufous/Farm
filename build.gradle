buildscript {
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2'
        classpath 'org.springframework:springloaded:1.2.6.RELEASE'
    }
}

plugins {
    id 'org.sonarqube' version '2.2.1'
}

allprojects {
    repositories {
        jcenter()
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Farm'
        property 'sonar.projectKey', 'project:farm'
        property 'sonar.projectVersion', '1.0'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.tests', 'application/src/test,databasetests/src/test'
        property 'sonar.dynamicAnalysis', 'reuseReports'
        property 'sonar.junit.reportsPaths', 'build/application/reports,build/databasetests/reports'
        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.jacoco.reportPaths', 'build/application/jacoco.exec,build/databasetests/jacoco.exec'
    }
}

configure([project(':application'), project(':databasetests')]) {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'jacoco'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
    }

    buildDir = file("${rootDir}/build/${name}")

    project(':application').bootRepackage.mustRunAfter tasks.findByName('check')
}

project(':application') {
    def mainClassName = 'com.farm.FarmApplication'

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

    jar {
        baseName = 'farm'
        manifest.attributes('Main-Class': mainClassName)
    }

    bootRepackage {
        mainClass = mainClassName
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.16.14'
        //Spring
        compile 'org.springframework.boot:spring-boot-devtools:1.3.0.RELEASE'
        compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.5.1.RELEASE'
        compile 'org.springframework.data:spring-data-jpa:1.11.0.RELEASE'
        compile 'org.springframework.boot:spring-boot-starter-web:1.5.1.RELEASE', {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile 'org.springframework.boot:spring-boot-starter-jetty:1.5.1.RELEASE'
        compile 'org.springframework.boot:spring-boot-starter-actuator:1.5.1.RELEASE'

        //database
        compile 'com.zaxxer:HikariCP:2.5.1'
        compile 'org.hibernate:hibernate-entitymanager:5.2.6.Final'
        compile 'org.hibernate:hibernate-validator:5.3.4.Final'
        compile 'org.hibernate:hibernate-core:5.2.6.Final'

        compile 'postgresql:postgresql:9.2-1002.jdbc4'
        compile 'mysql:mysql-connector-java:5.1.6'
        compile 'com.h2database:h2:1.4.194'
        compile 'org.liquibase:liquibase-core:3.5.3'


        //JavaEE
        compile 'javax:javaee-api:7.0'
        compile 'javax.el:javax.el-api:3.0.0'
        compile 'com.sun.el:el-ri:1.0'

        //3-rd path
        compile 'org.apache.commons:commons-collections4:4.1'

        //Tests
        //groovy
        testCompile 'org.codehaus.groovy:groovy-all:2.4.8'
        testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
        testCompile 'cglib:cglib:3.2.5'

        //Spring
        testCompile 'org.springframework.boot:spring-boot-test:1.5.1.RELEASE'
        testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.1.RELEASE'
        //hamcrest
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        //mockito
        compile 'org.mockito:mockito-all:1.10.19'


        //development
        compile 'org.springframework.boot:spring-boot-devtools:1.5.2.RELEASE'
    }
}